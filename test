.data
	# Gia su co toi da 250 de thi
	inOChu: .asciiz "Ket qua cua chuong trinh : "
	tbDoanDungOChu: .asciiz "Chuc mung ban da gianh chien thang \n"
	tbDoanSaiOChu: .asciiz "Ban da doan sai. Tro choi ket thuc\n"
	tbNhapKiTuDuDoan: .asciiz "Nhap ki tu du doan : "
	tbNhapOChuDuDoan: .asciiz "Nhap o chu du doan : "
	tbThucHienDoan: .asciiz "Chon kieu du doan\n1.Doan ki tu\n2.Doan o chu\nLua chon: "
	tbOChuDuDoan: .asciiz "O chu cua chuong trinh hom nay la : "
	tbNhapTen: .asciiz "Nhap ten : "
	tbTenKhongHopLe: .asciiz "Ten khong hop le\n"
	inTenNguoiChoi: .asciiz "Ten nguoi choi la : "
	inStop : .asciiz "Ham dung o day"
	inPhanTuHienTai: .asciiz "Phan tu hien tai la : "
	inPhanTuSau: .asciiz "Phan tu sau la : "
	inSoNgauNhien: .asciiz "So ngau nhien la : "
	tbBatDauChoi: .asciiz "Bat dau choi"
	tbKhoiTaoLai: .asciiz "Khoi tao lai"
	tbTroChoiKetThuc: .asciiz "Da thi het tat ca cac de. Tro choi ket thuc\n"
	tbCoMuonChoiTiep: .asciiz "Ban co muon choi tiep hay ko?\n0.Khong\n1.Co\nLua chon: "
	fin: .asciiz "input.txt"
	soDeThi: .word 0
	doDaiDeThi: .word 0
	soDeDaThi: .word 0 # So luong de da lam
	soLanSai: .word 0
	cacDeThi: .space 30
	dethi: .space 30
	mangDeDaThi: .space 1000 # Mang luu nhung de da thi
	tenNguoiChoi: .space 10
	oChuChuongTrinh: .space 30
	doanCaOChu: .space 30
	oChuTamThoi: .space 30
.text

main:
	# Khoi tao mangDeDaThi toan phan tu 0
	la $a0, mangDeDaThi
	li $a1, 1000
	jal _KhoiTaoMang
	lw $0, doDaiDeThi

	# Doc file de thi, luu nhung gi trong file vao bien cacDeThi, luu so de thi vao bien soDeThi
	jal _DocFileDeThi

	# Lay ten nguoi choi
	jal _NhapTen

main.TroChoiBatDau:
	# Kiem tra xem da co de thi chua, neu co thi khoi tao lai
	lb $t0, dethi
	bne $t0, $0, main.KhoiTaoLai

main.GoGo:
	# Lay de thi ngau nhien
	la $a0, mangDeDaThi
	jal _LayDeThiNgauNhien

	# Khoi tao o chu du doan
	la $a0, oChuChuongTrinh
	jal _KhoiTaoOChuDuDoan


	# Thuc hien du doan
	jal _ThucHienDuDoan

	#Hoi nguoi choi co muon choi tiep hay ko
	li $v0, 4
	la $a0, tbCoMuonChoiTiep
	syscall
	
	li $v0, 5
	syscall

	# Neu nhap vao 0 -> thoat chuong trinh
	beq $v0, $0, main.ThoatChuongTrinh
	j main.TroChoiBatDau

main.ThoatChuongTrinh:
	li $v0, 10
	syscall

main.KhoiTaoLai:
	# Khoi tao lai mang de thi va doDaiDeThi	
	la $a0, dethi
	li $a1, 30
	jal _KhoiTaoMang
	sw $0, doDaiDeThi
	j main.GoGo
	
#======= Thuc hien du doan ========#
_ThucHienDuDoan:
	#=== Dau ham
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $s5, 52($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)
	sw $t4, 40($sp)
	sw $t5, 44($sp)
	sw $t6, 48($sp)
	
	#=== Than ham
	# Khoi tao cac bien de thuc hien doan tung ki tu
	li $t1, 0 # Luu so lan doan sai
	lw $t5, doDaiDeThi # Luu do dai de thi
	li $s5, 0 # So ki tu doan dung

	la $a0, oChuTamThoi # Khoi tao mang o chu tam thoi moi
	li $a1, 30
	jal _KhoiTaoMang

	la $a0, oChuTamThoi # Khoi tao o chu du doan bang cach them cac dau *
	jal _KhoiTaoOChuDuDoan
	

	ChonCachDoan:
	la $a0, oChuTamThoi
	jal _ShowOChuDuDoan

	li $v0, 11
	li $a0, '\n'
	syscall

	li $v0, 4
	la $a0, tbThucHienDoan
	syscall

	li $v0, 5
	syscall

	#Luu lua chon vao $s0
	move $s0, $v0
	li $t0, 1	

	# Neu s0 = 1 -> doan ki tu, con khong thi doan o chu
	beq $s0, $t0, _ThucHien.DoanKiTu

	#=== Doan ca o chu ===#
	_ThucHien.DoanOChu:
	# Nhap o chu du doan
	li $v0, 4
	la $a0, tbNhapOChuDuDoan
	syscall

	li $v0, 8
	la $a0, doanCaOChu
	li $a1, 30
	syscall

	# kiem tra xem du doan dung ko
	li $t0, 0 # Bien dem
	li $t1, 0 # Luu ket qua kiem tra
	lw $s2, doDaiDeThi
	la $s0, doanCaOChu
	la $s1, dethi
	
	DoanOChu.Lap:
	bge $t0, $s2, _DuDoanOChu.Thang
	lb $t2, ($s0)
	lb $t3, ($s1)
	addi $t0, $t0, 1
	addi $s0, $s0, 1
	addi $s1, $s1, 1
	bne $t2, $t3, _DuDoanOChu.Thua
	j DoanOChu.Lap

	_DuDoanOChu.Thua: # Thua cuoc. Tro choi ket thuc
	li $v0, 11
	li $a0, '\n'
	syscall

	li $v0, 4
	la $a0, inOChu
	syscall

	li $v0, 4
	la $a0, dethi
	syscall

	li $v0, 11
	li $a0, '\n'
	syscall

	li $v0, 4
	la $a0, tbDoanSaiOChu
	syscall
	j _DuDoan.CuoiHam

	_DuDoanOChu.Thang: # Du doan dung toan bo o chu. Chien thang tro choi
	li $v0, 11
	li $a0, '\n'
	syscall

	li $v0, 11
	li $a0, '\n'
	syscall

	li $v0, 4
	la $a0, tbDoanDungOChu
	syscall

	li $v0, 11
	li $a0, '\n'
	syscall

	j _DuDoan.CuoiHam

	#===== Ket thuc phan du doan ca o chu ====#

	#== Thuc hien doan tung ki tu ==#
	_ThucHien.DoanKiTu:
	# Kiem tra trong de thi co ki tu nay khong
	li $t0, 0 # Bien dem lap ki tu
	li $t2, 0 # Bien dem kiem tra ki tu
	la $s1, dethi
	li $s4, 0
	la $s2, oChuTamThoi

	_DoanKiTu.LapNhapKiTu:
		beq $s4, 1, _DoanKiTu.NhapKiTu # Neu ki tu doan dung, thi nhap ki tu tiep
		addi $t1, $t1, 1    # Neu ki tu doan sai, tang so lan sai them 1
		bgt $t1, 7, ThuaCuoc # Neu sai qua 7 lan -> thua cuoc

		_DoanKiTu.NhapKiTu:
		li $s4, 0 # Luu ket qua cua viec kiem tra
		li $t2, 0 # Bien dem cua kiem tra ki tu

		li $v0, 4 
		la $a0, tbNhapKiTuDuDoan
		syscall

		li $v0, 12
		syscall
		# Luu ki tu du doan vao $s3
		move $s3, $v0
		la $s2, oChuTamThoi
		la $s1, dethi
		_DoanKiTu.KiemTraKiTu:
			# Kiem tra xem da kiem tra xong chua
			addi $t2, $t2, 1
			bgt $t2, $t5, ChonCachDoan

			lb $t3, ($s1)  # Luu dethi[i] vao $t3

			beq $s3, $t3, KiTuDung
			addi $s1, $s1, 1
			addi $s2, $s2, 1
			j _DoanKiTu.KiemTraKiTu

			KiTuDung:
			lb $t4, ($s2)
			bne $t4, '*', KiTuDung.Continue
			# Neu bang dau *, thi moi luu vao de thi, va tang so luong ki tu doan dung
			sb $s3, ($s2)
			addi $s5, $s5, 1 # +1 so ki tu dung
			beq $s5, $t5, _DuDoanOChu.Thang # Neu so tu doan dung = do dai de thi -> thang

			KiTuDung.Continue:
			li $s4, 1 # Cho biet ki tu nay da dung
			addi $s1, $s1, 1
			addi $s2, $s2, 1
			j _DoanKiTu.KiemTraKiTu

	ThuaCuoc:
		li $v0, 11
		li $a0, '\n'
		syscall

		li $v0, 4
		la $a0, inOChu
		syscall

		li $v0, 4
		la $a0, dethi
		syscall

		li $v0, 11
		li $a0, '\n'
		syscall

		li $v0, 4
		la $a0, tbDoanSaiOChu
		syscall

_DuDoan.CuoiHam:
	#=== Cuoi ham
	lw $ra, ($sp)
	lw $s0, 4($sp)
	lw $s1, 8($sp)
	lw $s2, 12($sp)
	lw $s3, 16($sp)
	lw $s4, 36($sp)
	lw $s5, 52($sp)
	lw $t0, 20($sp)
	lw $t1, 24($sp)
	lw $t2, 28($sp)
	lw $t3, 32($sp)
	lw $t4, 40($sp)
	lw $t5, 44($sp)
	lw $t6, 48($sp)
	addi $sp, $sp, 64
	jr $ra

	


#====== Khoi tao o chu du doan ******** =======#	
_KhoiTaoOChuDuDoan:
	#=== Dau ham
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)

	#=== Than ham
	li $t0, 0 # Bien dem
	lw $t1, doDaiDeThi
	li $t2, '*'
	move $s1, $a0

	_KhoiTaoOChu.lap:
	sb $t2, ($s1)
	addi $t0, $t0, 1
	addi $s1, $s1, 1
	blt $t0, $t1, _KhoiTaoOChu.lap

	#=== Cuoi ham
	lw $ra, ($sp)
	lw $s0, 4($sp)
	lw $s1, 8($sp)
	lw $s2, 12($sp)
	lw $s3, 16($sp)
	lw $s4, 36($sp)
	lw $t0, 20($sp)
	lw $t1, 24($sp)
	lw $t2, 28($sp)
	lw $t3, 32($sp)
	addi $sp, $sp, 64
	jr $ra

#===== Show o chu du doan ====== #
_ShowOChuDuDoan:
#=== Dau ham
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)

	move $s1, $a0

	#=== Than ham
	li $v0, 11
	li $a0, '\n'
	syscall

	li $t0, 0 # Bien dem
	lw $t1, doDaiDeThi


	li $v0, 4
	la $a0, tbOChuDuDoan
	syscall

	_ShowOChu.lap:
	lb $t2, ($s1)
	
	li $v0, 11
	move $a0, $t2
	syscall

	addi $t0, $t0, 1
	addi $s1, $s1, 1
	blt $t0, $t1, _ShowOChu.lap
	
	li $v0, 11
	li $a0, '\n'
	syscall
	
	#=== Cuoi ham
	lw $ra, ($sp)
	lw $s0, 4($sp)
	lw $s1, 8($sp)
	lw $s2, 12($sp)
	lw $s3, 16($sp)
	lw $s4, 36($sp)
	lw $t0, 20($sp)
	lw $t1, 24($sp)
	lw $t2, 28($sp)
	lw $t3, 32($sp)
	addi $sp, $sp, 64
	jr $ra

#========= Khoi tao mang (mang, so ki tu) ===========#
_KhoiTaoMang: # Khoi tao 250 phan tu trong mang = 0
#=== Dau ham
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)
#=== Than ham
	li $t0, 0
	move $s0, $a0 # Chuyen mang vao s0
	move $s1, $a1

_KhoiTaoMang.Lap:
	sb $0, ($s0)
	addi $t0, $t0, 1
	addi $s0, $s0, 1
	slt $t1, $t0, $s1
	beq $t1, 1, _KhoiTaoMang.Lap

#=== Cuoi ham
	lw $ra, ($sp)
	lw $s0, 4($sp)
	lw $s1, 8($sp)
	lw $s2, 12($sp)
	lw $s3, 16($sp)
	lw $s4, 36($sp)
	lw $t0, 20($sp)
	lw $t1, 24($sp)
	lw $t2, 28($sp)
	lw $t3, 32($sp)
	addi $sp, $sp, 64
	jr $ra

#======================= Ham nhap ten ==================================#
_NhapTen:
	# Dau ham
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)
	# Than ham
	_NhapTen.ThucHien: # Thuc hien viec nhap ten vao may tinh
	li $v0, 4
	la $a0, tbNhapTen
	syscall

	li $v0, 8
	la $a0, tenNguoiChoi
	li $a1, 10
	syscall

	la $s0, tenNguoiChoi
	li $t3, 0

	_NhapTen.KiemTra.Lap: # Kiem tra ten co hop le hay khong
	lb $t0, ($s0)
	
	# a[i] < 48 '0'
	blt $t0, '0', _NhapTen.KhongHopLe
	
	# a[i] > 57 '9'
	bgt $t0, '9',  _KiemTra.ChuHoa
	j _ChuCai.HopLe

	_KiemTra.ChuHoa:
	# a[i] < 65 'A'
	blt $t0, 'A', _NhapTen.KhongHopLe
	
	# a[i] > 90
	bgt $t0, 'Z',  _KiemTra.ChuThuong
	j _ChuCai.HopLe

	_KiemTra.ChuThuong:
	# a[i] < 97 'a'
	blt $t0, 'a', _NhapTen.KhongHopLe

	# a[i] > 122 'z'
	bgt $t0, 'z',  _NhapTen.KhongHopLe
	j _ChuCai.HopLe


	_ChuCai.HopLe: # Neu chu cai hop le
	li $t3, '\n'
	addi $s0, $s0, 1
	lb $t2, ($s0)
	beq $t2, $t3, _NhapTen.HopLe
	j _NhapTen.KiemTra.Lap

	_NhapTen.HopLe: # Ket thuc phan nhap ten, in ten nguoi nhap
	li $v0, 4
	la $a0, inTenNguoiChoi
	syscall

	li $v0, 4
	la $a0, tenNguoiChoi
	syscall


	# Cuoi ham
	lw $ra, ($sp)
	lw $s0, 4($sp)
	lw $s1, 8($sp)
	lw $s2, 12($sp)
	lw $s3, 16($sp)
	lw $s4, 36($sp)
	lw $t0, 20($sp)
	lw $t1, 24($sp)
	lw $t2, 28($sp)
	lw $t3, 32($sp)
	addi $sp, $sp, 64
	jr $ra

	_NhapTen.KhongHopLe:
	li $v0, 4
	la $a0, tbTenKhongHopLe
	syscall

	la $a0, tenNguoiChoi
	li $a1, 10
	jal _KhoiTaoMang

	j _NhapTen.ThucHien
	

#======================= Xuat mang (mang) ===========================================#
_XuatMang:

#Dau mang
	addi $sp, $sp, -32
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $t0, 12($sp)
	sw $t1, 16($sp)	

	move $s0, $a0
	move $s1, $a1

#Than mang
	li $t0, 0
XuatMang.Loop:
	li $v0, 1
	lw $a0, ($s1)
	syscall

	li $v0, 11
	li $a0, ' '
	syscall

	addi $s1, $s1, 4
	addi $t0, $t0, 1
	slt $t1, $t0, $s0
	beq $t1, 1, XuatMang.Loop

#Cuoi mang
	lw $ra, ($sp)
	lw $s0, 4($sp)
	lw $s1, 8($sp)
	lw $t0, 12($sp)
	lw $t1, 16($sp)
	addi $sp, $sp, 32
	jr $ra

#======== Ham doc file, lay so de thi ========================#
_DocFileDeThi:
#=====Dau ham
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)
#===== Than ham
	#openfile
	li $v0, 13
	la $a0, fin
	li $a1, 0 # Mo de doc
	li $a2, 0
	syscall

	#Sau khi open, $v0 chua file descriptor 

	#Luu dia chi file ds vao $s0
	move $s0, $v0 

	#Read file
	li $v0, 14
	move $a0, $s0
	la $a1, cacDeThi
	li $a2, 30
	syscall

	# s2 = so ki tu trong file
	move $s2, $v0

	#Dem so tu trong de thi, luu vao $s0
	li $s0, 1 # Bien luu ket qua so tu
	li $t0, 0 # Bien dem
	la $s1, cacDeThi
	li $t1, '*'

DemSoTu:
	lb $t2, ($s1) # $t2 luu ket qua hien tai
	addi $s1, $s1, 1
	addi $t0, $t0, 1
	
	# Neu ki tu = '\n'
	bne $t2, $t1, DemSoTu.Test
	addi $s0, $s0, 1 # Neu la ki tu '*', tang so tu len 1 	

DemSoTu.Test:
	# Kiem tra xem load het mang chua
	slt $t3, $t0, $s2
	beq $t3, 1, DemSoTu

	sw $s0, soDeThi

#===== Cuoi ham
	lw $ra, ($sp)
	lw $s0, 4($sp)
	lw $s1, 8($sp)
	lw $s2, 12($sp)
	lw $s3, 16($sp)
	lw $s4, 36($sp)
	lw $t0, 20($sp)
	lw $t1, 24($sp)
	lw $t2, 28($sp)
	lw $t3, 32($sp)
	addi $sp, $sp, 64
	jr $ra

#=================== Ham lay de thi ngau nhien ======================================#
_LayDeThiNgauNhien: # LayNgauNhien(mang de da thi) .Luu de thi vao $v0, luu do dai de thi vao $v1, luu so de thi vao $v2
#=====Dau ham
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $s5, 40($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)

#=====Than ham
	#Kiem tra xem da thi het 250 de hay chua, neu co thi ket thuc luon
	lw $s0, soDeDaThi
	lw $s1, soDeThi
	slt $t0, $s0, $s1
	beq $t0, 0, LayDeThi.ThiHetRoi 



TaoSoNgauNhienMoi:
	# Tao so ngau nhien luu vao $a0
	li $v0, 42
	lw $a1, soDeThi
	syscall 

	# s0 la so ngau nhien
	move $s0, $a0
	addi $s0, $s0, 1

	la $s4, mangDeDaThi
	# Kiem tra xem so ngau nhien do co bi trung vs cac de da thi hay ko
TaoSoNgauNhien.Trung:
	lw $t1, ($s4)
	beq $t1, $0, TaoSoNgauNhien.ThemSoVaoMang
	beq $t1, $s0, TaoSoNgauNhienMoi
	addi $s4, $s4, 4
	j TaoSoNgauNhien.Trung



TaoSoNgauNhien.ThemSoVaoMang:
	sw $s0, ($s4)

	#Tang so de da thi them 1
	lw $s2, soDeDaThi
	addi $s2, $s2, 1
	sw $s2, soDeDaThi

	
#=== Lay de thi ====#
	# s0 la so ngau nhien + 1
	li $t0, 1
	la $s1, cacDeThi
	la $s2, dethi
	li $s3, 0
	la $t2, '*'
	la $t3, '\0'
LayDeThi.loop:
	beq $t0, $s0, LayDeThi.LayTu # Neu i = n -> bat dau lay tu
	lb $t1, ($s1) #Lay tung ki tu so sanh vs *
	addi $s1, $s1, 1
	bne $t1, $t2, LayDeThi.loop
	addi $t0, $t0, 1
	j LayDeThi.loop

LayDeThi.LayTu:
	lb $t1, ($s1)

	beq $t1, $t2, LayDeThi.KetThuc # Neu = '*' hoac = '\0' -> ket thuc
	beqz $t1, LayDeThi.KetThuc
	sb $t1, ($s2) # Neu la ki tu binh thuong thi luu vao mang moi

	addi $s1, $s1, 1 #Tang cac bien dem, str
	addi $s2, $s2, 1
	addi $s3, $s3, 1

	j LayDeThi.LayTu

LayDeThi.KetThuc:
	# Luu do dai de thi vao s3, luu de thi vao s2 
	sw $s3, doDaiDeThi

#===== Cuoi ham
	lw $ra, ($sp)
	lw $s0, 4($sp)
	lw $s1, 8($sp)
	lw $s2, 12($sp)
	lw $s3, 16($sp)
	lw $s4, 36($sp)
	lw $s5, 40($sp)
	lw $t0, 20($sp)
	lw $t1, 24($sp)
	lw $t2, 28($sp)
	lw $t3, 32($sp)
	addi $sp, $sp, 64
	jr $ra

LayDeThi.ThiHetRoi:
	li $v0, 4
	la $a0, tbTroChoiKetThuc
	syscall

	li $v0, 10
	syscall

#==================== Ham gi do ===================================#
	


	
	

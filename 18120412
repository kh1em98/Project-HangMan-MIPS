.data
	# Cac trang thai treo co
	tt1: .asciiz "______\n|/   |\n|\n|\n|\n|\n"
	tt2: .asciiz "______\n|/   |\n|    0\n|\n|\n|\n"
	tt3: .asciiz "______\n|/   |\n|    0\n|    |\n|\n|\n"
	tt4: .asciiz "______\n|/   |\n|    0\n|   /|\n|\n|\n"
	tt5: .asciiz "______\n|/   |\n|    0\n|   /|\\\n|\n|\n"
	tt6: .asciiz "______\n|/   |\n|    0\n|   /|\\\n|   /\n|\n"
	tt7: .asciiz "______\n|/   |\n|    0\n|   /|\\\n|   / \\\n|\n"
	inOChu: .asciiz "Ket qua cua chuong trinh : "
	tbDoanDungOChu: .asciiz "Chuc mung ban da gianh chien thang"
	tbDoanSaiOChu: .asciiz "Ban da doan sai. Tro choi ket thuc\n"
	tbNhapKiTuDuDoan: .asciiz "Nhap ki tu du doan : "
	tbNhapOChuDuDoan: .asciiz "Nhap o chu du doan : "
	tbThucHienDoan: .asciiz "Chon kieu du doan\n1.Doan ki tu\n2.Doan o chu\nLua chon: "
	tbOChuDuDoan: .asciiz "O chu cua chuong trinh la : "
	tbNhapTen: .asciiz "Nhap ten : "
	tbTenKhongHopLe: .asciiz "Ten khong hop le\n"
	tbDanhSachNguoiChoi: .asciiz "Top 10 nguoi choi diem cao nhat : "
	tbKetQua: .asciiz "Ket qua cua ban la : "
	inDiem: .asciiz "Diem cua nguoi choi la "
	inTenNguoiChoi: .asciiz "Ten nguoi choi la : "
	inSoLuotChienThang: .asciiz "So luot choi chien thang : "
	tbBatDauChoi: .asciiz "Bat dau choi"
	tbTroChoiKetThuc: .asciiz "Da thi het tat ca cac de. Tro choi ket thuc\n"
	tbCoMuonChoiTiep: .asciiz "Ban co muon choi tiep hay ko?\n0.Khong\n1.Co\nLua chon: "

	fin: .asciiz "dethi.txt"
	fout: .asciiz "nguoichoi.txt"

	soLuotChienThang: .word 0
	soDeThi: .word 0
	doDaiDeThi: .word 0
	soDeDaThi: .word 0 # So luong de da lam
	soLanSai: .word 0
	diem: .word 0
	soNguoiChoi: .word 0

	mangDeDaThi: .space 1000
	cacNguoiChoi: .space 900
	mangNguoiChoi: .space 900
	cacDeThi: .space 900
	mangDiem: .space 120
	mangThuTu: .space 120
	dethi: .space 30
	tenNguoiChoi: .space 10
	oChuChuongTrinh: .space 30
	doanCaOChu: .space 30
	oChuTamThoi: .space 30
	thongTinLuu: .space 30
	chuoiInt: .space 30
	diemTam: .space 30

.text
main:
	# Doc file de thi, luu nhung gi trong file vao bien cacDeThi, luu so de thi vao bien soDeThi
	jal _DocFileDeThi

	# Lay ten nguoi choi
	jal _NhapTen


# Sau khi thua ma muon choi tiep
Thua.ChoiTiep:
	# Can khoi tao lai so diem, mang de da thi = rong~
	li $s0, 0 # s0 = 0 -> thua, s0 = 1 -> thang
	sw $s0, diem
	sw $s0, soLuotChienThang
	sw $s0, doDaiDeThi
	sw $s0, soDeDaThi
	li $s1, 0 # Luu so luot chien thang
	# Khoi tao mangDeDaThi toan phan tu 0
	la $a0, mangDeDaThi
	li $a1, 1000
	jal _KhoiTaoMangSoNguyen
	lw $0, doDaiDeThi

# Thang tro choi va choi tiep
Thang.ChoiTiep:
	# Kiem tra xem da co de thi chua, neu co thi khoi tao lai
	lb $t0, dethi
	bne $t0, $0, main.KhoiTaoLai

main.GoGo:
	# Lay de thi ngau nhien
	la $a0, mangDeDaThi
	jal _LayDeThiNgauNhien

	# Khoi tao o chu du doan
	la $a0, oChuChuongTrinh
	jal _KhoiTaoOChuDuDoan


	# Thuc hien du doan
	jal _ThucHienDuDoan

	# Luu ket qua thang thua vao $s0
	move $s0, $v0

	beqz $s0, DaThua
	
	# Neu thang
	addi $s1, $s1, 1
	sw $s1, soLuotChienThang

	li $v0, 4
	la $a0, inDiem
	syscall

	li $v0, 1
	lw $a0, diem
	syscall

	jal _XuongDong
	j Thang.ChoiTiep

	#Luu thong tin nguoi choi, hoi xem co muon choi tiep hay khong
	DaThua:
	li $v0, 4
	la $a0, tbOChuDuDoan
	syscall

	jal _XuongDong

	jal _LuuNguoiChoi

	jal _DocFileNguoiChoi

	jal _TaoMangDiem

	li $v0, 4
	la $a0, tbCoMuonChoiTiep
	syscall
	
	li $v0, 5
	syscall

	# Neu nhap vao 0 -> thoat chuong trinh
	beq $v0, $0, main.ThoatChuongTrinh
	j Thua.ChoiTiep

main.ThoatChuongTrinh:
	li $v0, 10
	syscall

main.KhoiTaoLai:
	# Khoi tao lai mang de thi va doDaiDeThi	
	la $a0, dethi
	li $a1, 30
	jal _KhoiTaoMang
	sw $0, doDaiDeThi
	j main.GoGo
	


#====== Ham xuong dong ====#
_XuongDong:
	#Dau ham
	addi $sp, $sp, -64
	sw $ra, ($sp)
	#Than ham
	li $v0, 11
	li $a0, '\n'
	syscall
	# Cuoi ham
	lw $ra, ($sp)
	addi $sp, $sp, 64
	jr $ra
	

#=== Chuyen so thanh ki tu, $a0 la so, $a1 la noi luu ky tu ==#
int2str:
    addi $sp, $sp, -4         # to avoid headaches save $t- registers used in this procedure on stack
    sw   $t0, ($sp)           # so the values don't change in the caller. We used only $t0 here, so save that.
    j    next0                # else, goto 'next0'

    neg_num:                  # body of "if num < 0:"
        li   $t0, '-'
        sb   $t0, ($a1)           # *str = ASCII of '-' 
        addi $a1, $a1, 1          # str++
        li   $t0, -1
        mul  $a0, $a0, $t0        # num *= -1

    next0:
        li   $t0, -1
        addi $sp, $sp, -4         # make space on stack
        sw   $t0, ($sp)           # and save -1 (end of stack marker) on MIPS stack

    push_digits:
        blez $a0, next1           # num < 0? If yes, end loop (goto 'next1')
        li   $t0, 10              # else, body of while loop here
        div  $a0, $t0             # do num / 10. LO = Quotient, HI = remainder
        mfhi $t0                  # $t0 = num % 10
        mflo $a0                  # num = num // 10  
        addi $sp, $sp, -4         # make space on stack
        sw   $t0, ($sp)           # store num % 10 calculated above on it
        j    push_digits          # and loop

    next1:
        lw   $t0, ($sp)           # $t0 = pop off "digit" from MIPS stack
        addi $sp, $sp, 4          # and 'restore' stack

        bltz $t0, neg_digit       # if digit <= 0, goto neg_digit (i.e, num = 0)
        j    pop_digits           # else goto popping in a loop

    neg_digit:
        li   $t0, '0'
        sb   $t0, ($a1)           # *str = ASCII of '0'
        addi $a1, $a1, 1          # str++
        j    next2                # jump to next2

    pop_digits:
        bltz $t0, next2           # if digit <= 0 goto next2 (end of loop)
        addi $t0, $t0, '0'        # else, $t0 = ASCII of digit
        sb   $t0, ($a1)           # *str = ASCII of digit
        addi $a1, $a1, 1          # str++
        lw   $t0, ($sp)           # digit = pop off from MIPS stack 
        addi $sp, $sp, 4          # restore stack
        j    pop_digits           # and loop

    next2:
        sb  $zero, ($a1)          # *str = 0 (end of string marker)

        lw   $t0, ($sp)           # restore $t0 value before function was called
        addi $sp, $sp, 4          # restore stack
        jr  $ra                   # jump to caller

#===== Chuyen string thanh int ======#
str2int:
	#Dau ham 
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $s5, 52($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)
	sw $t4, 40($sp)
	sw $t5, 44($sp)
	sw $t6, 48($sp)

	# Than ham
	la $s1, diemTam
	li $t0, 10
	li $s2, 0

	lp:         
  	lb $t1, ($s1)       #load unsigned char from array into t1
  	beq $t1, $0, str2int.end     #NULL terminator found
  	addi $t1, $t1, -48   #converts t1's ascii value to dec value
  	mul $s2, $s2, $t0    #sum *= 10
  	add $s2, $s2, $t1    #sum += array[s1]-'0'
  	addi $s1, $s1, 1     #increment array address
  	j lp                 #jump to start of loop

	str2int.end:
	move $v0, $s2
	# Cuoi ham
        lw $ra, ($sp)
        lw $s0, 4($sp)
        lw $s1, 8($sp)
        lw $s2, 12($sp)
        lw $s3, 16($sp)
        lw $s4, 36($sp)
        lw $s5, 52($sp)
        lw $t0, 20($sp)
        lw $t1, 24($sp)
        lw $t2, 28($sp)
        lw $t3, 32($sp)
        lw $t4, 40($sp)
        lw $t5, 44($sp)
        lw $t6, 48($sp)
        addi $sp, $sp, 64
        jr $ra

# Viet ham strlen
_Strlen:
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $s5, 52($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)
	sw $t4, 40($sp)
	sw $t5, 44($sp)
	sw $t6, 48($sp)

	move $s0, $a0
	li $t0, 0
	_Strlen.Lap:
	lb $t1, ($s0)
	beqz $t1, _Strlen.End
	addi $t0, $t0, 1
	addi $s0, $s0, 1
	j _Strlen.Lap


	_Strlen.End:
	move $v0, $t0
	lw $ra, ($sp)
	lw $s0, 4($sp)
	lw $s1, 8($sp)
	lw $s2, 12($sp)
	lw $s3, 16($sp)
	lw $s4, 36($sp)
	lw $s5, 52($sp)
	lw $t0, 20($sp)
	lw $t1, 24($sp)
	lw $t2, 28($sp)
	lw $t3, 32($sp)
	lw $t4, 40($sp)
	lw $t5, 44($sp)
	lw $t6, 48($sp)
	addi $sp, $sp, 64
	jr $ra
	


#=== Viet diem va ten nguoi choi vao file ====#
_LuuNguoiChoi:
	#Dau ham
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $s5, 52($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)
	sw $t4, 40($sp)
	sw $t5, 44($sp)
	sw $t6, 48($sp)
	#Than ham
	la $a0, chuoiInt
	li $a1, 30
	jal _KhoiTaoMang

	la $a0, thongTinLuu
	li $a1, 30
	jal _KhoiTaoMang

	lw $s0, diem
	la $s1, tenNguoiChoi
	lw $s2, soLuotChienThang
	la $s3, thongTinLuu
	li $t0, 0
	li $t4, '\n'

	LuuNguoiChoi.Lap:
        lb $t1, ($s1)
        beq $t1, $t4, LuuNguoiChoi.ContinueDiem

        sb $t1, ($s3)

        addi $s1, $s1, 1
        addi $s3, $s3, 1
        addi $t0, $t0, 1

        j LuuNguoiChoi.Lap

	LuuNguoiChoi.ContinueDiem:
        li $t5, ' '
        sb $t5, ($s3)
        addi $s3, $s3, 1
        addi $t0, $t0, 1
        
        # Chuyen diem thanh string, tinh do dai string do, sau do them vao chuoi thong tin luu
        lw $a0, diem
        la $a1, chuoiInt
        jal int2str

        la $s1, chuoiInt

	
	LuuNguoiChoi.Diem:
        lb $t1, ($s1)
        beq $t1, $0, LuuNguoiChoi.ContinueSoLuot

        sb $t1, ($s3)
        addi $s1, $s1, 1
        addi $s3, $s3, 1
        addi $t0, $t0, 1
        j LuuNguoiChoi.Diem

	LuuNguoiChoi.ContinueSoLuot:
        li $t5, ' '
        sb $t5, ($s3)
        addi $s3, $s3, 1
        addi $t0, $t0, 1

        lw $a0, soLuotChienThang
        la $a1, chuoiInt
        jal int2str

        la $s1, chuoiInt
	LuuNguoiChoi.SoLuot:
        lb $t1, ($s1)
        beq $t1, $0, LuuNguoiChoi.VietVaoFile

        sb $t1, ($s3)
        addi $s1, $s1, 1
        addi $s3, $s3, 1
        addi $t0, $t0, 1
        j LuuNguoiChoi.SoLuot


	LuuNguoiChoi.VietVaoFile:
        li $t5, '\n'
        sb $t5, ($s3)
        addi $s3, $s3, 1
        addi $t0, $t0, 1

	li $v0, 13
	la $a0, fout
	li $a1, 9
	li $a2, 0
	syscall
	move $s6, $v0

	li $v0, 15       
  	move $a0, $s6      
  	la $a1, thongTinLuu
  	move $a2, $t0 
  	syscall          

	# Dong file
	li $v0, 16
	move $a0, $s6
	syscall

	li $v0, 4
	la $a0, tbKetQua
	syscall
	
	li $v0, 4
	la $a0, thongTinLuu
	syscall

	Luu.End:
        #Cuoi ham
        lw $ra, ($sp)
        lw $s0, 4($sp)
        lw $s1, 8($sp)
        lw $s2, 12($sp)
        lw $s3, 16($sp)
        lw $s4, 36($sp)
        lw $s5, 52($sp)
        lw $t0, 20($sp)
        lw $t1, 24($sp)
        lw $t2, 28($sp)
        lw $t3, 32($sp)
        lw $t4, 40($sp)
        lw $t5, 44($sp)
        lw $t6, 48($sp)
        addi $sp, $sp, 64
        jr $ra
	

#======= Show trang thai(int number) =======#
_ShowTrangThai:
	#Dau ham
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $s5, 52($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)
	sw $t4, 40($sp)
	sw $t5, 44($sp)
	sw $t6, 48($sp)
	move $s0, $a0
	#Than ham
	beq $s0, 1, _ShowTrangThai.0
	beq $s0, 2, _ShowTrangThai.1
	beq $s0, 3, _ShowTrangThai.2
	beq $s0, 4, _ShowTrangThai.3
	beq $s0, 5, _ShowTrangThai.4
	beq $s0, 6, _ShowTrangThai.5
	beq $s0, 7, _ShowTrangThai.6
	beq $s0, 8, _ShowTrangThai.7

	_ShowTrangThai.0:
	    j _ShowTrangThai.End

	_ShowTrangThai.1:
    	jal _XuongDong
        li $v0, 4,
        la $a0, tt1
        syscall
    	jal _XuongDong
        j _ShowTrangThai.End

	_ShowTrangThai.2:
    	jal _XuongDong
        li $v0, 4,
        la $a0, tt2
        syscall
   	jal _XuongDong
        j _ShowTrangThai.End

	_ShowTrangThai.3:
    	jal _XuongDong
        li $v0, 4,
        la $a0, tt3
        syscall
   	jal _XuongDong
        j _ShowTrangThai.End

	_ShowTrangThai.4:
    	jal _XuongDong
        li $v0, 4,
        la $a0, tt4
        syscall
    	jal _XuongDong
        j _ShowTrangThai.End

	_ShowTrangThai.5:
    	jal _XuongDong
        li $v0, 4,
        la $a0, tt5
        syscall
    	jal _XuongDong
        j _ShowTrangThai.End

	_ShowTrangThai.6:
    	jal _XuongDong
        li $v0, 4,
        la $a0, tt6
        syscall
    	jal _XuongDong
        j _ShowTrangThai.End

	_ShowTrangThai.7:
    	jal _XuongDong
        li $v0, 4,
        la $a0, tt7
        syscall
    	jal _XuongDong 
        j _ShowTrangThai.End


	_ShowTrangThai.End:
        #Cuoi ham
        lw $ra, ($sp)
        lw $s0, 4($sp)
        lw $s1, 8($sp)
        lw $s2, 12($sp)
        lw $s3, 16($sp)
        lw $s4, 36($sp)
        lw $s5, 52($sp)
        lw $t0, 20($sp)
        lw $t1, 24($sp)
        lw $t2, 28($sp)
        lw $t3, 32($sp)
        lw $t4, 40($sp)
        lw $t5, 44($sp)
        lw $t6, 48($sp)
        addi $sp, $sp, 64
        jr $ra

#===== Khoi tao mang so nguyen =====#
_KhoiTaoMangSoNguyen:
	#Dau ham 
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $s5, 52($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)
	sw $t4, 40($sp)
	sw $t5, 44($sp)
	sw $t6, 48($sp)

	# Than ham
	la $s0, mangDeDaThi
	li $s1, 250
	li $t0, 0
	_KhoiTaoMangSoNguyen.Lap:
	bge $t0, $s1, _KhoiTaoMangSoNguyen.End
	sw $0, ($s0)
	addi $s0, $s0, 4
	addi $t0, $t0, 1
	j _KhoiTaoMangSoNguyen.Lap

	_KhoiTaoMangSoNguyen.End:
	# Cuoi ham
        lw $ra, ($sp)
        lw $s0, 4($sp)
        lw $s1, 8($sp)
        lw $s2, 12($sp)
        lw $s3, 16($sp)
        lw $s4, 36($sp)
        lw $s5, 52($sp)
        lw $t0, 20($sp)
        lw $t1, 24($sp)
        lw $t2, 28($sp)
        lw $t3, 32($sp)
        lw $t4, 40($sp)
        lw $t5, 44($sp)
        lw $t6, 48($sp)
        addi $sp, $sp, 64
        jr $ra

#======= Thuc hien du doan ========#
_ThucHienDuDoan:
	#=== Dau ham
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $s5, 52($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)
	sw $t4, 40($sp)
	sw $t5, 44($sp)
	sw $t6, 48($sp)
	sw $t7, 56($sp)
	
	#=== Than ham
	# Khoi tao cac bien de thuc hien doan tung ki tu
	li $t7, 0 # Luu so luot chien thang
	li $t1, 0 # Luu so lan doan sai
	lw $t5, doDaiDeThi # Luu do dai de thi
	li $s5, 0 # So ki tu doan dung
	li $s4, 0 # Kiem tra xem ki tu truoc doan co dung ko

	la $a0, oChuTamThoi # Khoi tao mang o chu tam thoi moi
	li $a1, 30
	jal _KhoiTaoMang

	la $a0, oChuTamThoi # Khoi tao o chu du doan bang cach them cac dau *
	jal _KhoiTaoOChuDuDoan
	

	ChonCachDoan:
        addi $t7, $t7, 1
        beq $s4, 1, ChonCachDoan.continue # Neu ki tu doan dung, thi nhap ki tu tiep
        addi $t1, $t1, 1    # Neu ki tu doan sai, tang so lan sai them 1	

        bgt $t1, 7, ThuaCuoc # Neu sai qua 7 lan -> thua cuoc
        
        # Show trang thai neu doan sai
        move $a0, $t1
        jal _ShowTrangThai
	
	ChonCachDoan.continue:
        la $a0, oChuTamThoi
        jal _ShowOChuDuDoan
        
    	jal _XuongDong

        li $v0, 4
        la $a0, tbThucHienDoan
        syscall

        li $v0, 5
        syscall

        #Luu lua chon vao $s0
        move $s0, $v0	

        # Neu s0 = 1 -> doan ki tu, con khong thi doan o chu
        beq $s0, 1, _ThucHien.DoanKiTu

	#=== Doan ca o chu ===#
	_ThucHien.DoanOChu:
        # Nhap o chu du doan
        li $v0, 4
        la $a0, tbNhapOChuDuDoan
        syscall

        li $v0, 8
        la $a0, doanCaOChu
        li $a1, 30
        syscall

        # kiem tra xem du doan dung ko
        li $t0, 0 # Bien dem
        li $t1, 0 # Luu ket qua kiem tra
        lw $s2, doDaiDeThi
        la $s0, doanCaOChu
        la $s1, dethi
	
	DoanOChu.Lap:
        bge $t0, $s2, _DuDoanOChu.Thang
        lb $t2, ($s0)
        lb $t3, ($s1)
        addi $t0, $t0, 1
        addi $s0, $s0, 1
        addi $s1, $s1, 1
        bne $t2, $t3, ThuaCuoc
        j DoanOChu.Lap

	_DuDoanOChu.Thang: # Du doan dung toan bo o chu. Chien thang tro choi. In thong bao doan dung o chu va tong so luot de gianh chien thang
        lw $t6, diem
        lw $s2, doDaiDeThi
        add $t6, $t6, $s2
        sw $t6, diem

            jal _XuongDong
        li $v0, 4
        la $a0, tbDoanDungOChu
        syscall
            jal _XuongDong

            jal _XuongDong

        li $v0, 1

        j _DuDoan.CuoiHam

	#===== Ket thuc phan du doan ca o chu ====#

	#== Thuc hien doan tung ki tu ==#
	_ThucHien.DoanKiTu:
        # Kiem tra trong de thi co ki tu nay khong
        li $t0, 0 # Bien dem lap ki tu
        li $t2, 0 # Bien dem kiem tra ki tu
        la $s1, dethi
        li $s4, 0
        la $s2, oChuTamThoi

        _DoanKiTu.LapNhapKiTu:
            _DoanKiTu.NhapKiTu:
                li $s4, 0 # Luu ket qua cua viec kiem tra
                li $t2, 0 # Bien dem cua kiem tra ki tu

                li $v0, 4 
                la $a0, tbNhapKiTuDuDoan
                syscall

                li $v0, 12
                syscall
                # Luu ki tu du doan vao $s3
                move $s3, $v0
                la $s2, oChuTamThoi
                la $s1, dethi
            _DoanKiTu.KiemTraKiTu:
                # Kiem tra xem da kiem tra xong chua
                addi $t2, $t2, 1
                bgt $t2, $t5, ChonCachDoan

                lb $t3, ($s1)  # Luu dethi[i] vao $t3

                beq $s3, $t3, KiTuDung
                addi $s1, $s1, 1
                addi $s2, $s2, 1
                j _DoanKiTu.KiemTraKiTu

                KiTuDung:
                    lb $t4, ($s2)
                    bne $t4, '*', KiTuDung.Continue
                    # Neu bang dau *, thi moi luu vao de thi, va tang so luong ki tu doan dung
                    sb $s3, ($s2)
                    addi $s5, $s5, 1 # +1 so ki tu dung
                    beq $s5, $t5, _DuDoanOChu.Thang # Neu so tu doan dung = do dai de thi -> thang

                KiTuDung.Continue:
                    li $s4, 1 # Cho biet ki tu nay da dung
                    addi $s1, $s1, 1
                    addi $s2, $s2, 1
                    j _DoanKiTu.KiemTraKiTu

	ThuaCuoc:
		li $a0, 8
		jal _ShowTrangThai

		li $v0, 4
		la $a0, tbDoanSaiOChu
		syscall

		li $v0, 0

    _DuDoan.CuoiHam:

        #=== Cuoi ham
        lw $ra, ($sp)
        lw $s0, 4($sp)
        lw $s1, 8($sp)
        lw $s2, 12($sp)
        lw $s3, 16($sp)
        lw $s4, 36($sp)
        lw $s5, 52($sp)
        lw $t0, 20($sp)
        lw $t1, 24($sp)
        lw $t2, 28($sp)
        lw $t3, 32($sp)
        lw $t4, 40($sp)
        lw $t5, 44($sp)
        lw $t6, 48($sp)
        lw $t7, 56($sp)
        addi $sp, $sp, 64
        jr $ra

	


#====== Khoi tao o chu du doan ******** =======#	
_KhoiTaoOChuDuDoan:
	#=== Dau ham
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)

	#=== Than ham
	li $t0, 0 # Bien dem
	lw $t1, doDaiDeThi
	li $t2, '*'
	move $s1, $a0

	_KhoiTaoOChu.lap:
        sb $t2, ($s1)
        addi $t0, $t0, 1
        addi $s1, $s1, 1
        blt $t0, $t1, _KhoiTaoOChu.lap

	#=== Cuoi ham
	lw $ra, ($sp)
	lw $s0, 4($sp)
	lw $s1, 8($sp)
	lw $s2, 12($sp)
	lw $s3, 16($sp)
	lw $s4, 36($sp)
	lw $t0, 20($sp)
	lw $t1, 24($sp)
	lw $t2, 28($sp)
	lw $t3, 32($sp)
	addi $sp, $sp, 64
	jr $ra

#===== Show o chu du doan ====== #
_ShowOChuDuDoan:
#=== Dau ham
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)

	move $s1, $a0

	#=== Than ham
	li $v0, 11
	li $a0, '\n'
	syscall

	li $t0, 0 # Bien dem
	lw $t1, doDaiDeThi


	li $v0, 4
	la $a0, tbOChuDuDoan
	syscall

	_ShowOChu.lap:
        lb $t2, ($s1)
        
        li $v0, 11
        move $a0, $t2
        syscall

        addi $t0, $t0, 1
        addi $s1, $s1, 1
        blt $t0, $t1, _ShowOChu.lap
        
    jal _XuongDong
	
	#=== Cuoi ham
	lw $ra, ($sp)
	lw $s0, 4($sp)
	lw $s1, 8($sp)
	lw $s2, 12($sp)
	lw $s3, 16($sp)
	lw $s4, 36($sp)
	lw $t0, 20($sp)
	lw $t1, 24($sp)
	lw $t2, 28($sp)
	lw $t3, 32($sp)
	addi $sp, $sp, 64
	jr $ra

#========= Khoi tao mang (mang, so ki tu) ===========#
_KhoiTaoMang: # Khoi tao 250 phan tu trong mang = 0
#=== Dau ham
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)
#=== Than ham
	li $t0, 0
	move $s0, $a0 # Chuyen mang vao s0
	move $s1, $a1

    _KhoiTaoMang.Lap:
        sb $0, ($s0)
        addi $t0, $t0, 1
        addi $s0, $s0, 1
        slt $t1, $t0, $s1
        beq $t1, 1, _KhoiTaoMang.Lap

#=== Cuoi ham
	lw $ra, ($sp)
	lw $s0, 4($sp)
	lw $s1, 8($sp)
	lw $s2, 12($sp)
	lw $s3, 16($sp)
	lw $s4, 36($sp)
	lw $t0, 20($sp)
	lw $t1, 24($sp)
	lw $t2, 28($sp)
	lw $t3, 32($sp)
	addi $sp, $sp, 64
	jr $ra

#======================= Ham nhap ten ==================================#
_NhapTen:
	# Dau ham
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)
	# Than ham
	_NhapTen.ThucHien: # Thuc hien viec nhap ten vao may tinh
        li $v0, 4
        la $a0, tbNhapTen
        syscall

        li $v0, 8
        la $a0, tenNguoiChoi
        li $a1, 10
        syscall

        la $s0, tenNguoiChoi
        li $t3, 0

	_NhapTen.KiemTra.Lap: # Kiem tra ten co hop le hay khong
        lb $t0, ($s0)
        
        # a[i] < 48 '0'
        blt $t0, '0', _NhapTen.KhongHopLe
        
        # a[i] > 57 '9'
        bgt $t0, '9',  _KiemTra.ChuHoa
        j _ChuCai.HopLe

	_KiemTra.ChuHoa:
        # a[i] < 65 'A'
        blt $t0, 'A', _NhapTen.KhongHopLe
        
        # a[i] > 90
        bgt $t0, 'Z',  _KiemTra.ChuThuong
        j _ChuCai.HopLe

	_KiemTra.ChuThuong:
        # a[i] < 97 'a'
        blt $t0, 'a', _NhapTen.KhongHopLe

        # a[i] > 122 'z'
        bgt $t0, 'z',  _NhapTen.KhongHopLe
        j _ChuCai.HopLe


	_ChuCai.HopLe: # Neu chu cai hop le
        li $t3, '\n'
        addi $s0, $s0, 1
        lb $t2, ($s0)
        beq $t2, $t3, _NhapTen.HopLe
        j _NhapTen.KiemTra.Lap

	_NhapTen.HopLe: # Ket thuc phan nhap ten, in ten nguoi nhap
        li $v0, 4
        la $a0, inTenNguoiChoi
        syscall

        li $v0, 4
        la $a0, tenNguoiChoi
        syscall


	# Cuoi ham
	lw $ra, ($sp)
	lw $s0, 4($sp)
	lw $s1, 8($sp)
	lw $s2, 12($sp)
	lw $s3, 16($sp)
	lw $s4, 36($sp)
	lw $t0, 20($sp)
	lw $t1, 24($sp)
	lw $t2, 28($sp)
	lw $t3, 32($sp)
	addi $sp, $sp, 64
	jr $ra

	_NhapTen.KhongHopLe:
        li $v0, 4
        la $a0, tbTenKhongHopLe
        syscall

        la $a0, tenNguoiChoi
        li $a1, 10
        jal _KhoiTaoMang

        j _NhapTen.ThucHien
	

#======================= Xuat mang (so phan tu,mang) ===========================================#
_XuatMang:

#Dau mang
	addi $sp, $sp, -32
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $t0, 12($sp)
	sw $t1, 16($sp)	

	move $s0, $a0
	move $s1, $a1

#Than mang
	li $t0, 0
    XuatMang.Loop:
        li $v0, 1
        lw $a0, ($s1)
        syscall

        li $v0, 11
        li $a0, ' '
        syscall

        addi $s1, $s1, 4
        addi $t0, $t0, 1
        slt $t1, $t0, $s0
        beq $t1, 1, XuatMang.Loop

    #Cuoi mang
        lw $ra, ($sp)
        lw $s0, 4($sp)
        lw $s1, 8($sp)
        lw $t0, 12($sp)
        lw $t1, 16($sp)
        addi $sp, $sp, 32
        jr $ra

#======== Ham doc file, lay so de thi ========================#
_DocFileDeThi:
#=====Dau ham
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)
#===== Than ham
	#openfile
	li $v0, 13
	la $a0, fin
	li $a1, 0 # Mo de doc
	li $a2, 0
	syscall

	#Sau khi open, $v0 chua file descriptor 

	#Luu dia chi file ds vao $s0
	move $s0, $v0 

	#Read file
	li $v0, 14
	move $a0, $s0
	la $a1, cacDeThi
	li $a2, 900
	syscall

	# s2 = so ki tu trong file
	move $s2, $v0

	# Close file
	li $v0, 16
	move $a0, $s0
	syscall

	#Dem so tu trong de thi, luu vao $s0
	li $s0, 1 # Bien luu ket qua so tu
	li $t0, 0 # Bien dem
	la $s1, cacDeThi
	li $t1, '*'

    DemSoTu:
        lb $t2, ($s1) # $t2 luu ket qua hien tai
        addi $s1, $s1, 1
        addi $t0, $t0, 1
        
        # Neu ki tu = '\n'
        bne $t2, $t1, DemSoTu.Test
        addi $s0, $s0, 1 # Neu la ki tu '*', tang so tu len 1 	

    DemSoTu.Test:
        # Kiem tra xem load het mang chua
        slt $t3, $t0, $s2
        beq $t3, 1, DemSoTu

        sw $s0, soDeThi

#===== Cuoi ham
	lw $ra, ($sp)
	lw $s0, 4($sp)
	lw $s1, 8($sp)
	lw $s2, 12($sp)
	lw $s3, 16($sp)
	lw $s4, 36($sp)
	lw $t0, 20($sp)
	lw $t1, 24($sp)
	lw $t2, 28($sp)
	lw $t3, 32($sp)
	addi $sp, $sp, 64
	jr $ra



#=================== Ham lay de thi ngau nhien ======================================#
_LayDeThiNgauNhien: 
	# Dau ham
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $s5, 40($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)

	# Than ham
	#Kiem tra xem da thi het de hay chua, neu co thi ket thuc luon
	lw $s0, soDeDaThi
	lw $s1, soDeThi
	slt $t0, $s0, $s1
	beq $t0, 0, LayDeThi.ThiHetRoi 



    TaoSoNgauNhienMoi:
        # Tao so ngau nhien luu vao $a0
        li $v0, 42
        lw $a1, soDeThi
        syscall 

        # Tang so ngau nhien len 1
        move $s0, $a0
        addi $s0, $s0, 1

        la $s4, mangDeDaThi

        # Kiem tra xem so ngau nhien do co bi trung vs cac de da thi hay ko
    TaoSoNgauNhien.Trung:
        lw $t1, ($s4)
        beq $t1, $0, TaoSoNgauNhien.ThemSoVaoMang
        beq $t1, $s0, TaoSoNgauNhienMoi
        addi $s4, $s4, 4
        j TaoSoNgauNhien.Trung



    TaoSoNgauNhien.ThemSoVaoMang:
        sw $s0, ($s4)

        #Tang so de da thi them 1
        lw $s2, soDeDaThi
        addi $s2, $s2, 1
        sw $s2, soDeDaThi

        
    #=== Lay de thi ====#
        # s0 la so ngau nhien + 1
        li $t0, 1
        la $s1, cacDeThi
        la $s2, dethi
        li $s3, 0
        la $t2, '*'
        la $t3, '\0'
    LayDeThi.loop:
        beq $t0, $s0, LayDeThi.LayTu # Neu i = n -> bat dau lay tu
        lb $t1, ($s1) #Lay tung ki tu so sanh vs *
        addi $s1, $s1, 1
        bne $t1, $t2, LayDeThi.loop
        addi $t0, $t0, 1
        j LayDeThi.loop

    LayDeThi.LayTu:
        lb $t1, ($s1)
        beq $t1, $t2, LayDeThi.KetThuc # Neu = '*' hoac = '\0' -> ket thuc
        beqz $t1, LayDeThi.KetThuc
        sb $t1, ($s2) # Neu la ki tu binh thuong thi luu vao mang moi

        addi $s1, $s1, 1 #Tang cac bien dem, str
        addi $s2, $s2, 1
        addi $s3, $s3, 1

        j LayDeThi.LayTu

    LayDeThi.KetThuc:
        # Luu do dai de thi vao s3, luu de thi vao s2 
        sw $s3, doDaiDeThi

	

    # Cuoi ham
        lw $ra, ($sp)
        lw $s0, 4($sp)
        lw $s1, 8($sp)
        lw $s2, 12($sp)
        lw $s3, 16($sp)
        lw $s4, 36($sp)
        lw $s5, 40($sp)
        lw $t0, 20($sp)
        lw $t1, 24($sp)
        lw $t2, 28($sp)
        lw $t3, 32($sp)
        addi $sp, $sp, 64
        jr $ra

    LayDeThi.ThiHetRoi:
        jal _LuuNguoiChoi

        li $v0, 4
        la $a0, tbTroChoiKetThuc
        syscall

        li $v0, 10
        syscall

#============ Doc file nguoi choi, luu vao mangNguoiChoi =================#
_DocFileNguoiChoi:
	#Dau ham
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $s5, 52($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)
	sw $t4, 40($sp)
	sw $t5, 44($sp)
	sw $t6, 48($sp)
	#Than ham
	la $a0, cacNguoiChoi
	li $a1, 900
	jal _KhoiTaoMang
	sw $0, soNguoiChoi

	#openfile
	li $v0, 13
	la $a0, fout
	li $a1, 0 # Mo de doc
	li $a2, 0
	syscall

	#Sau khi open, $v0 chua file descriptor 

	#Luu dia chi file ds vao $s0
	move $s0, $v0 

	#Read file
	li $v0, 14
	move $a0, $s0
	la $a1, cacNguoiChoi
	li $a2, 900
	syscall

	# Dong file
	li $v0, 16
	move $a0, $s0
	syscall

	#Dem so nguoi choi, luu vao $s0
	li $t0, 0 # Bien dem
	la $s0, cacNguoiChoi 
	la $s1, mangNguoiChoi
	li $t1, '\n'
	li $t3, 0
	li $t4, 30

	TaoMangNguoiChoi:
        lb $t2, ($s0)
        beqz $t2, TaoMangNguoiChoi.KetThuc
        beq $t2, $t1, TaoMangNguoiChoi.TangPhanTu

        sb $t2, ($s1)
        addi $s0, $s0, 1
        addi $s1, $s1, 1
        addi $t3, $t3 ,1
        j TaoMangNguoiChoi

	TaoMangNguoiChoi.TangPhanTu:
        sub $t3, $t4, $t3
        li $t5, 0
        li $t2, '*'
        LapDayPhanTu:
            bge $t5, $t3, TaoMangNguoiChoi.TangPhanTu.Continue
            sb $t2, ($s1)
            addi $s1, $s1, 1
            addi $t5, $t5, 1
            j LapDayPhanTu

	TaoMangNguoiChoi.TangPhanTu.Continue:
        addi $s0, $s0, 1
        addi $t0, $t0, 1
        li $t3, 0
        j TaoMangNguoiChoi
	
	TaoMangNguoiChoi.KetThuc:
	    sw $t0, soNguoiChoi
	
	#Cuoi ham
        jal _XuongDong
	
	jal _TaoMangThuTu

	lw $ra, ($sp)
	lw $s0, 4($sp)
	lw $s1, 8($sp)
	lw $s2, 12($sp)
	lw $s3, 16($sp)
	lw $s4, 36($sp)
	lw $s5, 52($sp)
	lw $t0, 20($sp)
	lw $t1, 24($sp)
	lw $t2, 28($sp)
	lw $t3, 32($sp)
	lw $t4, 40($sp)
	lw $t5, 44($sp)
	lw $t6, 48($sp)
	addi $sp, $sp, 64
	jr $ra

#====== In mang nguoi choi ======#
_InMangNguoiChoi:
	#Dau ham 
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $s5, 52($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)
	sw $t4, 40($sp)
	sw $t5, 44($sp)
	sw $t6, 48($sp)
	# Than ham
	li $t0, 0
	lw $t1, soNguoiChoi
	la $s0, mangNguoiChoi
	la $s1, mangNguoiChoi
	
	_InMangNguoiChoi.Lap:
        lb $t2, ($s0)
        beqz $t2, _InMangNguoiChoi.PhanTuMoi
        li $v0, 11
        move $a0, $t2
        syscall
        addi $s0, $s0, 1
        j _InMangNguoiChoi.Lap
	
	_InMangNguoiChoi.PhanTuMoi:
        addi $t0, $t0, 1
        bge $t0, $t1, _InMangNguoiChoi.KetThuc
            jal _XuongDong
        addi $s1, $s1, 30
        move $s0, $s1
        j _InMangNguoiChoi.Lap

	_InMangNguoiChoi.KetThuc:
        # Cuoi ham
        lw $ra, ($sp)
        lw $s0, 4($sp)
        lw $s1, 8($sp)
        lw $s2, 12($sp)
        lw $s3, 16($sp)
        lw $s4, 36($sp)
        lw $s5, 52($sp)
        lw $t0, 20($sp)
        lw $t1, 24($sp)
        lw $t2, 28($sp)
        lw $t3, 32($sp)
        lw $t4, 40($sp)
        lw $t5, 44($sp)
        lw $t6, 48($sp)
        addi $sp, $sp, 64
        jr $ra

#====== Tao mang thu tu phuc vu sap xep =====#
_TaoMangThuTu:
	#Dau ham 
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $s5, 52($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)
	sw $t4, 40($sp)
	sw $t5, 44($sp)
	sw $t6, 48($sp)

	# Than ham
	la $a0, mangThuTu
	li $a1, 120
	jal _KhoiTaoMang

	lw $s0, soNguoiChoi
	la $s1, mangThuTu
	li $t0, 0
	
	_TaoMangThuTu.Lap:
		bge $t0, $s0, _TaoMangThuTu.End
		sw $t0, ($s1)
		addi $t0, $t0, 1
		addi $s1, $s1, 4
		j _TaoMangThuTu.Lap

	_TaoMangThuTu.End:
	# Cuoi ham
        lw $ra, ($sp)
        lw $s0, 4($sp)
        lw $s1, 8($sp)
        lw $s2, 12($sp)
        lw $s3, 16($sp)
        lw $s4, 36($sp)
        lw $s5, 52($sp)
        lw $t0, 20($sp)
        lw $t1, 24($sp)
        lw $t2, 28($sp)
        lw $t3, 32($sp)
        lw $t4, 40($sp)
        lw $t5, 44($sp)
        lw $t6, 48($sp)
        addi $sp, $sp, 64
        jr $ra
	
	
#==== Tao mang diem =====#
_TaoMangDiem:
	#Dau ham 
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $s5, 52($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)
	sw $t4, 40($sp)
	sw $t5, 44($sp)
	sw $t6, 48($sp)

	# Than ham
	la $a0, mangDiem
	li $a1, 120
	jal _KhoiTaoMang

	li $t0, 0
	li $t1, 0
	li $t2, ' '
	lw $s0, soNguoiChoi
	la $s1, cacNguoiChoi
	la $s2, mangDiem
	la $s3, diemTam

	_TaoMangDiem.Lap:
		bge $t0, $s0, _TaoMangDiem.End
		lb $t3, ($s1)
		beq $t3, $t2, _TaoMangDiem.GapDauCach
		beq $t1, 1, _TaoMangDiem.LuuDiem
		addi $s1, $s1, 1
		j _TaoMangDiem.Lap

		_TaoMangDiem.LuuDiem:
			sb $t3, ($s3)
			addi $s3, $s3, 1
			addi $s1, $s1, 1
			j _TaoMangDiem.Lap

		_TaoMangDiem.GapDauCach:
			beq $t1, 0, _TaoMangDiem.BatDauLuu
			# Neu dang luu diem
			
			# Chuyen diemTam thanh diemthat, luu vao mang diem
			jal str2int
			move $s5, $v0
			sw $s5, ($s2)
			addi $s2, $s2, 4
			
			# Reset lai bien diem tam
			la $a0, diemTam
			li $a1, 30
			jal _KhoiTaoMang
			la $s3, diemTam


			li $t1, 0
			addi $s1, $s1, 1
			addi $t0, $t0, 1
			j _TaoMangDiem.Lap
			
			# Neu gap dau cach nhung chua luu diem thi bat dau luu
			 _TaoMangDiem.BatDauLuu:
			li $t1, 1
			addi $s1, $s1, 1
			j _TaoMangDiem.Lap

	_TaoMangDiem.End:
	# Xuat mang diem
	jal _SapXepMangDiem

	jal _InMangNguoiChoiSapXep

	# Cuoi ham
        lw $ra, ($sp)
        lw $s0, 4($sp)
        lw $s1, 8($sp)
        lw $s2, 12($sp)
        lw $s3, 16($sp)
        lw $s4, 36($sp)
        lw $s5, 52($sp)
        lw $t0, 20($sp)
        lw $t1, 24($sp)
        lw $t2, 28($sp)
        lw $t3, 32($sp)
        lw $t4, 40($sp)
        lw $t5, 44($sp)
        lw $t6, 48($sp)
        addi $sp, $sp, 64
        jr $ra

#==== Sap xep mang diem, doi thu tu =====#
_SapXepMangDiem:
	#Dau ham 
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $s5, 52($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)
	sw $t4, 40($sp)
	sw $t5, 44($sp)
	sw $t6, 48($sp)
	# Than ham
	li $t1, 1
	lw $t2, soNguoiChoi
	ble $t2, 1, _SapXepMangDiem.End
	li $t1, 4
	mult $t2, $t1
	mflo $a0
	addi $a0, $a0, -4
	la $t0, mangDiem
	add $t0, $t0, $a0

outterLoop:
	# t1 la boolean cho biet mang da sap xep xong chua
	add $t1, $0, $0
	la $s0, mangDiem
	la $s1, mangThuTu

innerLoop:
	lw $t2, ($s0)
	lw $t3, 4($s0)
	#So sanh xem so truoc co lon hon so sau ko, neu co thi swap, khong thi tiep tuc
	slt $t4, $t2, $t3
	beq $t4, 0, continue
	#Swap 2 so, t1 = false
	addi $t1, $0, 1
	sw $t2, 4($s0)
	sw $t3, ($s0)
	lw $t5, ($s1)
	lw $t6, 4($s1)
	sw $t5, 4($s1)
	sw $t6, ($s1)

continue:
	addi $s0, $s0, 4
	addi $s1, $s1, 4
	bne $s0, $t0, innerLoop #Neu chua phai ki tu cuoi mang
	bne $t1, $0, outterLoop #Neu van can phai swap

	_SapXepMangDiem.End:
	# Cuoi ham
        lw $ra, ($sp)
        lw $s0, 4($sp)
        lw $s1, 8($sp)
        lw $s2, 12($sp)
        lw $s3, 16($sp)
        lw $s4, 36($sp)
        lw $s5, 52($sp)
        lw $t0, 20($sp)
        lw $t1, 24($sp)
        lw $t2, 28($sp)
        lw $t3, 32($sp)
        lw $t4, 40($sp)
        lw $t5, 44($sp)
        lw $t6, 48($sp)
        addi $sp, $sp, 64
        jr $ra


#===== In mang diem sap xep =======#
_InMangNguoiChoiSapXep:
	#Dau ham 
	addi $sp, $sp, -64
	sw $ra, ($sp)
	sw $s0, 4($sp)
	sw $s1, 8($sp)
	sw $s2, 12($sp)
	sw $s3, 16($sp)
	sw $s4, 36($sp)
	sw $s5, 52($sp)
	sw $t0, 20($sp)
	sw $t1, 24($sp)
	sw $t2, 28($sp)
	sw $t3, 32($sp)
	sw $t4, 40($sp)
	sw $t5, 44($sp)
	sw $t6, 48($sp)

	# Than ham
	li $v0, 4
	la $a0, tbDanhSachNguoiChoi
	syscall
	jal _XuongDong

	li $t0, 10
	lw $s0, soNguoiChoi

	ble $s0, $t0, _InMangNguoiChoiSapXep.ThucHienIn
	# Neu nhieu hon 10 nguoi choi, gan toi da = 10
	li $s0, 10

_InMangNguoiChoiSapXep.ThucHienIn:
	la $s1, mangNguoiChoi
	la $s2, mangNguoiChoi
	la $s3, mangThuTu
	li $t0, 0
	li $t1, 0
	li $t2, '*'
	li $t3, 30
	lw $t4, ($s3)
	mult $t4, $t3
	mflo $t4
	add $s1, $s1, $t4

	_InMangNguoiChoiSapXep.Lap:
		bge $t0, $s0, _InMangNguoiChoiSapXep.End
		lb $t3, ($s1)
		beq $t3, $t2, _InMangNguoiChoiSapXep.TangPhanTu
		# Neu khong phai dau * thi in ki tu ra
		li $v0, 11
		move $a0, $t3
		syscall
		addi $s1, $s1, 1
		j _InMangNguoiChoiSapXep.Lap

	_InMangNguoiChoiSapXep.TangPhanTu:
		addi $s3, $s3, 4
		li $t3, 30
		lw $t4, ($s3)
		mult $t4, $t3
		mflo $t4
		la $s1, mangNguoiChoi
		add $s1, $s1, $t4
		addi $t0, $t0, 1
		jal _XuongDong
		j _InMangNguoiChoiSapXep.Lap
		
		
	_InMangNguoiChoiSapXep.End:
	jal _XuongDong
	# Cuoi ham
        lw $ra, ($sp)
        lw $s0, 4($sp)
        lw $s1, 8($sp)
        lw $s2, 12($sp)
        lw $s3, 16($sp)
        lw $s4, 36($sp)
        lw $s5, 52($sp)
        lw $t0, 20($sp)
        lw $t1, 24($sp)
        lw $t2, 28($sp)
        lw $t3, 32($sp)
        lw $t4, 40($sp)
        lw $t5, 44($sp)
        lw $t6, 48($sp)
        addi $sp, $sp, 64
        jr $ra
